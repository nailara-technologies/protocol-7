# >:]

# name = base.set_privs
# todo = cleanup!!

my $noop = ( defined $_[1] and $_[1] eq 'noop' ) ? 1 : 0;

my $usr = shift;

if ( !length( $usr // '' ) ) {
    <[base.log]>->( 0, 'can not change priviledges [no username supplied]' );
    return -1;
} else {

    my ( $name, $pwd, $uid, $gid, $quota, $comment, $gcos, $home_dir, $shell )
        = getpwnam($usr);

    my %_gid = map { $_ => 1 } split( / /, $( );
    my %egid = map { $_ => 1 } split( / /, $) );

    if (    !$noop
        and defined $name
        and exists $_gid{$gid}
        and exists $egid{$gid}    # XXX: fix (secondary groups)!
        and $> == $uid and $< == $uid
        ) {
        <[base.log]>->( 1, "set_privs: already am '$usr', doing nothing." );
        return -2;
    } elsif ($<) {
        <[base.log]>->(
            0, "can not change privs to user '$usr' [i am not root]"
        );
        return -1;
    } else {
        if ( not defined $name or $name ne $usr ) {
            <[base.log]>->(
                0, "can not change priviledges to '$usr' [no such user]"
            );
            return -1;
        } else {
            my @rgids;
            while ( my ( $_name, $_pw, $_gid, $_members ) = getgrent ) {
                push( @rgids, $_gid )
                    if ( grep( $_ eq $usr, split( /\s+/, $_members ) ) );
            }

            if ( length( $home_dir // '' ) and !-d $home_dir ) {
                <[base.log]>->(
                    0,
                    "<!> user '$usr' home dir is missing"
                        . " [creating $home_dir]"
                );
                make_path(
                    $home_dir,
                    {   'mode'  => 0700,
                        'uid'   => $uid,
                        'group' => $gid
                    }
                );
            }

            if ( !-d $home_dir ) {
                <[base.log]>->(
                    0, "failed to create home directory '$home_dir'"
                );
                $home_dir = '/tmp/.home_' . $name;
                if ( -d $home_dir ) {
                    <[base.log]>->(
                        1, ": using temporary home dir '$home_dir'.."
                    );
                } else {
                    <[base.log]>->(
                        1, ": creating temporary home dir '$home_dir' .."
                    );
                    make_path(
                        $home_dir,
                        {   'mode' => 0700,
                            'uid'  => $uid,
                            'gid'  => $gid
                        }
                    );
                }
            }

            return {
                'uid'      => $uid,
                'gid'      => $gid,
                'all_gids' => join( ' ', $gid, @rgids ),
                'home'     => $home_dir
                }
                if $noop;

            $) = join( ' ', $gid, @rgids );
            $( = $gid;
            $> = $uid;
            $< = $uid;

            $ENV{'USER'}            = $name;
            $ENV{'HOME'}            = $home_dir;
            $ENV{'XDG_RUNTIME_DIR'} = $home_dir;
            $ENV{'NO_AT_BRIDGE'}    = 1;
            chdir($home_dir) or warn "chdir($home_dir): $!\n";

            <[base.log]>->( 1, "changed priviledges to user '$usr'" );
        }
    }
}
