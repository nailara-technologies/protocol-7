# >:]

# name = base.set_privs
# todo = more cleanup.,

my $noop = ( defined $_[1] and $_[1] eq 'noop' ) ? 1 : 0;

my $usr = shift;

<[base.check_dependency_dirs]>->($usr);

if ( !length( $usr // '' ) ) {
    <[base.log]>->( 0, 'can not change priviledges [ no user name supplied ]' );
    return -1;
} else {

    my ( $name, $pwd, $uid, $gid, $quota, $comment, $gcos, $home_dir, $shell )
        = getpwnam($usr);

    my %_gid = map { $_ => 1 } split( / /, $( );
    my %egid = map { $_ => 1 } split( / /, $) );

    if (    !$noop
        and defined $name
        and exists $_gid{$gid}
        and exists $egid{$gid}    # LLL: complete secondary group[s] setup..,
        and $> == $uid and $< == $uid
    ) {
        <[base.log]>->( 1, "set_privs: already am '$usr', doing nothing." );
        return -2;
    } elsif ($<) {
        <[base.log]>->( 0, "can not change privs to user '$usr' [ not root ]" );
        return -1;
    } else {
        if ( not defined $name or $name ne $usr ) {
            <[base.log]>->(
                0, "can not change priviledges to '$usr' [ no such user ]"
            );
            return -1;
        } else {
            my @rgids;
            while ( my ( $_name, $_pw, $_gid, $_members ) = getgrent ) {
                push( @rgids, $_gid )
                    if ( grep( $_ eq $usr, split( /\s+/, $_members ) ) );
            }

            if ( !length( $home_dir // '' ) ) {
                <[base.log]>->(
                    0,
                    sprintf( "<!> undefined homedir [ user '%s' ] <!>", $usr )
                );
                <[base.log]>->( 2, " :. setting to '/'.," );
                $home_dir = '/';
            } elsif ( !-d $home_dir and $home_dir ne '/nonexistent' ) {
                <[base.log]>->(
                    0,
                    sprintf(
                        "<!> user '%s' has no home dir [ creating %s ]",
                        $usr, $home_dir
                    )
                );
                make_path(
                    $home_dir,
                    {   'mode'  => 0700,
                        'uid'   => $uid,
                        'group' => $gid
                    }
                );
            } elsif ( !-d $home_dir or $usr eq 'nobody' ) {    # /nonexistent
                <[base.log]>->(
                    0, "unable to create home directory '$home_dir'"
                ) if $home_dir ne '/nonexistent';
                ### temporary home directory ###
                my $tdir_template
                    = <system.agent.name> ne 'nroot'
                    ? sprintf "T.h.%s.%s.%d", <system.agent.name>, $usr, $$
                    : sprintf "T.h.%s.XXXXXXXXXX", $usr;
                $home_dir = <[base.temp_dir]>->( '/tmp/', $tdir_template );
                if ( -d $home_dir ) {
                    chown( $uid, $gid, $home_dir )
                        or warn "chown($uid,$gid,$home_dir): $!";
                    <[base.log]>->( 1, ":: created temporary home directory" );
                    <[base.log]>->( 1, " : $home_dir" );
                    push(
                        @{<system.init_reports>},
                        {   'target_command' => 'nroot.report-temp-path',
                            'param'          => $home_dir,
                            'descr' => 'registering temporary directory'
                        }
                    );
                }
            }

            return {
                'uid'      => $uid,
                'gid'      => $gid,
                'all_gids' => join( ' ', $gid, @rgids ),
                'home'     => $home_dir
                }
                if $noop;

            $) = join( ' ', $gid, @rgids );
            $( = $gid;
            $> = $uid;
            $< = $uid;

            $ENV{'USER'}            = $name;
            $ENV{'HOME'}            = $home_dir;
            $ENV{'XDG_RUNTIME_DIR'} = $home_dir;
            $ENV{'NO_AT_BRIDGE'}    = 1;
            chdir($home_dir) or warn "chdir( $home_dir ): $!\n";

            <[base.log]>->( 1, "changed priviledges to user '$usr'" );
        }
    }
}
