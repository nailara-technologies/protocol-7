# >:]

# name  = xserver.cmd.set_opacity
# param = <id> <opacity>
# descr = set window opacity in percent [0..100]

my $X = <xserver.x11>;
my ( $window_id, $opacity ) = split( / +/, $$call{'args'} );

return { 'mode' => 'nack', 'data' => 'window <id> required' }
    if not defined $window_id;
return { 'mode' => 'nack', 'data' => 'invalid window <id> syntax' }
    if $window_id !~ /^\d+$/;
return { 'mode' => 'nack', 'data' => 'window <opacity> required' }
    if not defined $opacity;
return { 'mode' => 'nack', 'data' => 'invalid window <opacity> [0..100]' }
    if $opacity !~ /^\d+$/ or $opacity > 100;

<xserver.atom.opacity> //= $X->InternAtom( '_NET_WM_WINDOW_OPACITY', 0 );
<xserver.const.cardinal> //= X11::AtomConstants::CARDINAL();

my $xtops = <xserver.x11_tops>;
$xtops->update;

my $window;
for my $w ( @{ $xtops->sorted } ) {
    if ( $w->id eq $window_id ) {
        $window = $w;
        last;
    }
}

return { 'mode' => 'nack', 'data' => 'no such window' } if not defined $window;

<[base.log]>->( 2, "setting window $window_id opacity to $opacity%" );

eval {
    $X->ChangeProperty( $window_id, <xserver.atom.opacity>,
        <xserver.const.cardinal>, 32, 'Replace',
        pack( 'I*', sprintf( "%.0f", 0xffffffff * $opacity / 100 ) ) );

    $X->GetProperty( $window_id, <xserver.atom.opacity>,
        X11::AtomConstants::CARDINAL(),
        0, 1, 0 );
};
<[base.log]>->(
    0, "error during set_opacity($opacity) for window $window_id [closed?]"
) if $@;

return { 'mode' => 'ack', 'data' => "set opacity to $opacity%" };
