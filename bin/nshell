#!/usr/bin/perl

BEGIN { $ENV{'PERL_RL'} = 'Perl' }

$0 = 'nshell ';

use utf8;
use strict;
use warnings;
use IO::Socket;
use IO::Socket::INET;
use IO::Socket::UNIX;
use Term::ReadPassword;
use Term::ReadLine::Perl;
use POSIX ":sys_wait_h";
use Digest::MD5 qw(md5_hex);

$SIG{'PIPE'} = 'IGNORE';
binmode STDOUT, ":utf8";

$SIG{'CHLD'} = sub {
    waitpid( -1, WNOHANG );
    $0 .= "[disconnected] ";
};

my @args;
my $remember  = 0;
my $arg_index = 0;
foreach my $param (@ARGV) {
    if ( $param =~ /^(-r|--remember)$/ ) {
        $remember = 1;
    } else {
        push( @args, $param );
    }
}

my $core_user;
my $core_pass;
my $sckargcnt = scalar @args;
my $core_addr = shift(@args) || '127.0.0.1';
my $core_port = shift(@args) || 242;
my $usock_dir = '/tmp/.n/sock';

my $color  = "\e[37m\e[40m";
my $prompt = $color . ':. ';

my $connect = 1;
my $WARNED  = 0;

$| = 0;

# clear();

my $term = new Term::ReadLine 'nailara.shell';

no warnings 'once';
$readline::rl_completion_function = "main::rl_filename_list_lcl";
use warnings 'once';

my $shell_sock;
while ($connect) {

    $shell_sock
        = shell_auth( shell_connect( $core_addr, $core_port ), login_prompt() );

    if ( -S $shell_sock ) {
        clear();
        $0 = "nshell ( $core_addr : $core_port ) ";
        shell_loop( $shell_sock, $prompt );
    }
}

sub login_prompt {
    if (    $remember
        and defined $core_user
        and defined $core_pass ) {
        return ( $core_user, $core_pass );
    }
    my ( $user, $pass );
    print "\n :: user :. ";
    chop( $user = <STDIN> );
    $pass = read_password(" :: pass :. ");
    print "\n";
    ( $core_user, $core_pass ) = ( $user, $pass ) if $remember;
    return ( $user, $pass );
}

sub shell_connect {
    my $sock;
    if ( !$sckargcnt ) {
        my $sock_path = $usock_dir . '/' . md5_hex("$core_addr:$core_port");
        if ( -e $sock_path ) {
            $sock = IO::Socket::UNIX->new(
                Type => SOCK_STREAM(),
                Peer => $sock_path,
            ) or warn "\n [!] failed to connect to unix:$sock_path [$!]\n";
        }
    }
    if ( not defined $sock ) {
        $sock = new IO::Socket::INET(
            PeerAddr => $core_addr,
            PeerPort => $core_port,
            Proto    => 'tcp'
            )
            or die "\n [!] failed to connect to $core_addr:$core_port [$!]\n\n";
    }
    die " [!] connection failed [protocol error]\n" if <$sock> !~ />:\]/;
    return $sock;
}

sub shell_auth {
    syswrite( $_[0], "auth $_[1] $_[2]\n" );
    sysread( $_[0], my $answer, 8 );
    if ( $answer =~ /YEAH/ ) {
        print " :: authentication successful ::\n";
        return $_[0];
    } elsif ( $answer =~ /FAIL >:\|/ ) {
        die " :: authentication failed ::\n\n";
    } elsif ( $answer =~ />:\|/ ) {
        print " :: authentication timeout ::\n";
        return 0;
    } else {
        die " :: protocol error ::\n\n";
    }
}

sub clear { print "\e[37m\e[40m\e[H\e[2J" }

sub shell_loop {
    my $sock   = $_[0];
    my $prompt = $_[1];

    if ( !fork() ) { stdout_fork(); exit }

    while ( defined( my $line = $term->readline($prompt) ) and -S $sock ) {
        $WARNED = 0;
        print $color;

        if ( $line ne '' ) {
            if ( $line =~ /^\.cd ([^\s]+)/ ) {
                if ( !chdir($1) ) { print "cd: " . $! . "\n" }
            } elsif ( $line !~ /^\.\./ and $line =~ /^\.(.+)$/ ) {
                if ( my $err = system($1) ) {
                    print "error code: " . $err . "\n";
                }
            } else {

                if ( !syswrite( $sock, "$line\n" ) ) {
                    select( undef, undef, undef, 0.1 );
                    exit;
                }
                select( undef, undef, undef, 0.2 );
                if ( $line eq '[quit]' or $line =~ "ACK session closed\n" ) {
                    exit;
                } elsif ( !-S $sock or !$sock->connected ) {
                    print "\e[2H\e[2J :: reconnecting...\n";
                    last;
                }

            }
        } elsif ( !-S $sock or !syswrite( $sock, "\n" ) or !$sock->connected ) {
            print "\e[2H\e[2J :: reconnecting...\n";
            last;
        }
    }
}

sub stdout_fork {
    close(STDIN);
    $0 = "nshell ( stdout ) ";
    while ( my $out = <$shell_sock> ) {
        $out =~ s/\n$//;
        print "$out\n";
    }
    print ":\n:..[ connection terminated ]\n\n";
}

sub rl_filename_list_lcl {
    my ( $text, $line, $pos ) = @_;

    if ( !$pos or $line !~ /^\./ ) {
        no warnings 'once';
        $readline::rl_completer_terminator_character = '';
        use warnings 'once';
        if ( !$WARNED and $line !~ /^\./ ) {
            print "\n[!] local shell commands need '.' prefix!\n";
            $WARNED = 1;
        }
        return '';
    }

    my @files = (<$text*>);
    foreach (@files) {
        if ( -d $_ ) {
            next if $pos == 0 and !-x $_;
            $_ .= '/';
        }
    }
    no warnings 'once';
    $readline::rl_completer_terminator_character = '';
    use warnings 'once';
    return @files;
}
