#!/usr/bin/perl

###          ###
##  question  ##
###          ###

use v5.24;
use strict;
use English;
use warnings;
use File::Spec;
use Cwd qw| abs_path |;
use FindBin qw| $RealBin |;
use Crypt::Misc qw| encode_b32r |;
our $up_dir     = File::Spec->updir;
our $Bin        = $RealBin;
our $FALSE_code = 202;
our %COLORS     = init_colors();

my $check_truth_bin = get_bin_path();
error_exit('expected question') if @ARGV == 0;

# ########################################################################## #

( my $question = join( ' ', @ARGV, qw| ? | ) ) =~ s|(\s*\?)+$| ?|;

my $question_encoded = sprintf( 'Q: %s : %s A!', network_time(), $question );

my $exit_code = system( $check_truth_bin, qw| -silent |, $question_encoded );
$exit_code >>= 8 if $exit_code > 0;

if ( $exit_code == 0 ) {
    true_answer($question);
    exit(0);
} elsif ( $exit_code == $FALSE_code ) {
    false_answer($question);
    exit($FALSE_code);
} else {
    error_exit("invovation error : is-true returned $exit_code");
}

# ########################################################################## #

sub network_time { return encode_b32r( pack( qw| w* |, calc_ntime() ) ) }

sub calc_ntime {    # [ network time : secs from 2002-06-05 * 4200 ]
    my $precision      = 5;
    my $ntime_start    = 1023228000;
    my $unix_precision = $precision + 2;
    my $unix_time      = sprintf qw| %.*f |, $unix_precision, time();
    my $ntime          = sprintf qw| %.*f |,
        $precision, ( ( $unix_time - $ntime_start ) * 4200 );

    return $ntime;
}

sub init_colors {
    return (
        'rst' => "\e[0m",
        'bl'  => "\e[1m",
        'bg'  => "\e[48;2;9;5;42m",
        'fT'  => "\e[38;2;6;71;195m",
        'fg'  => "\e[38;2;68;39;172m",
        'no'  => "\e[38;2;197;141;7m",
        'ng'  => "\e[38;2;71;195;6m"
    );
}

sub true_answer {

    my $question = shift;

    say "$COLORS{'fg'}:\n:: $COLORS{'bl'}"
        . "$COLORS{'fT'}TRUE$COLORS{'rst'}$COLORS{'fg'} :: $COLORS{'fT'}"
        . $question
        . "$COLORS{'fg'} .:\n:$COLORS{'rst'}";
}

sub false_answer {

    my $question = shift;

    say "$COLORS{'fg'}:\n:: $COLORS{'ng'}"
        . "$COLORS{'bl'}NOPE$COLORS{'rst'}$COLORS{'fg'} :: $COLORS{'fg'}"
        . $question
        . "$COLORS{'fg'} :.\n:$COLORS{'rst'}";
}

sub error_exit {

    my $err_str = shift;

    say "$COLORS{'fg'}:\n:: $COLORS{'bl'}"
        . "$COLORS{'no'}$err_str$COLORS{'rst'}"
        . "$COLORS{'fg'} ::\n:$COLORS{'rst'}";

    exit(4);
}

sub get_bin_path {
    my $bin_path = abs_path( r2_abs( c_dir($Bin) ) );
    die "\n:\n:: not found : $bin_path\n:\n" if not -d $bin_path;
    my $check_truth_path = abs_path( c_file( $bin_path, qw| is-true | ) );
    die "\n:\n:: not found : $check_truth_path\n:\n" if !-x $check_truth_path;
    return $check_truth_path;
}

sub c_dir  { File::Spec->catdir(@ARG) }
sub c_file { File::Spec->catfile(@ARG) }
sub r2_abs { File::Spec->rel2abs(@ARG) }

#,,..,...,,,.,.,.,,.,,,.,,,,,,,,.,,,.,,.,,,.,,..,,...,...,...,.,.,.,.,..,,..,,
#SJBUGUFHOUGQE6QJEYDM2SRLOSABCDXZ7AVCMLYOG4WN2AVGO6W4FHCSNPDYIKZE33FCZOMUTEMAQ
#\\\|SCJJIAITFYFB77UOF6QAV5KVJBHLCF2S32GS5LMC4FHS5UZW6GX \ / AMOS7 \ YOURUM ::
#\[7]F35ZCPNBBEEBDNGCP7TDJBYH53NJRKLHYCM5RR5WPDEI4K7N7WAI 7  DATA SIGNATURE ::
#:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
