#!/usr/bin/perl
use v5.24;
use strict;
use English;
use warnings;
use File::Spec;
use Cwd qw| abs_path |;
use FindBin qw| $RealBin |;
use Crypt::Misc qw| decode_b32r |;

my $ver_seed = 54;
my $release_version;

my $mode = ( @ARGV and $ARGV[0] eq '-m' ) ? 'msg_str' : 'rel_ver';
if ( @ARGV and $ARGV[0] eq '-s' ) { $mode = 'set_tag'; shift @ARGV }

if (    $mode eq 'set_tag'
    and @ARGV
    and $ARGV[0] =~ m|^(AMOS-)?v?(\d+\.\d+\.\d+)$| ) {
    $release_version = sprintf( "AMOS-v%s", $LAST_PAREN_MATCH )
        and shift @ARGV;    ## <-- setting custom rel. version ##
}

my $u = File::Spec->updir;
my $root_path
    = abs_path(
    File::Spec->rel2abs( File::Spec->catdir( $RealBin, $u, $u ) ) );
my $harmony_bin
    = File::Spec->catfile( File::Spec->catdir( $root_path, 'bin' ),
    qw| harmony | );
my $version_file
    = File::Spec->catfile( File::Spec->catdir( $root_path, 'configuration' ),
    qw| protocol-7.src-ver | );

die ":\n:: 'harmony' binary not found\n\n" if !-x $harmony_bin;

{
    local $| = 1;
    local $/ = undef;
    if ( open( my $v_fh, '<' . $version_file ) ) {
        ( my $src_ver_str = <$v_fh> ) =~ s|\s.+$||sg;

        ( my $ntime_B32, my $commit ) = split( m|\-|, $src_ver_str, 2 );

        my $ntime = ntime_dec($ntime_B32);

        if ( not defined $release_version ) {
            (   $release_version = sprintf( "AMOS-v%.3f",
                    ( $commit * $ntime / $ver_seed / ( 7777777 * 12242707 ) )
                )
            ) =~ s|(\d)$|\.$1|;
        }

        my $tag_msg
            = sprintf(
            "<< rel \\\\// p7-AMOS-base-v%s || base-code \\\\// %s >>",
            $release_version, $src_ver_str );
        if ( $mode eq 'rel_ver' ) {
            print STDOUT ":\n: calculated release version : ";
            print STDERR "$release_version\n";
            print STDOUT ":\n";

        } elsif ( $mode eq 'msg_str' ) {
            print "'$tag_msg'\n";
        } elsif ( $mode eq 'set_tag' ) {
            print ":\n: setting release tag [ '$release_version' ]\n:\n::";
            system( 'git', 'tag', "$release_version", '-m', $tag_msg, @ARGV )
                && die "::<<!>>:\n\n";
            print ":\n\n";
        }
    } else {
        die "$version_file: $!\n";
    }
}

sub ntime_dec {
    my $network_time = shift;

    die ":\n:: invalid network time format in 'src-ver' file\n\n"
        if $network_time !~ m|^[A-Z2-7]{1,22}$|;

    my $ntime_dec;
    eval { $ntime_dec = unpack( 'w', decode_b32r($network_time) ) };

    die ":\n:: network time decoding not successful\n\n"
        if $EVAL_ERROR
        or !length( $ntime_dec // '' );

    return $ntime_dec;
}

#.............................................................................
#Z4FUYRQHEXM6PVA7VPLSSANIWGZASDUV6YQI4FZDOSPWPC7CLUMMVPMCBLQSDCDY737U4NUGJJBUY
#::: DIJLZGHWUMOUPZHE7PYI2EICT6CZ2XA42FQF76NPXKPHDZP7QXO :::: NAILARA AMOS :::
# :: UNLCXLAFNCLRA2QPYVSGXMKYK4P7WP3ZKJ2KRNRF4QERUHBUOMCQ :: CODE SIGNATURE ::
# ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
